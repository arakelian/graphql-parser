// graphql-parser

buildscript {
    repositories {
      jcenter()
    }
}

plugins {
	id 'java'
	id 'maven'
    id 'signing'
	id 'eclipse'
	id 'idea'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.20.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '1.2.18'

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.12.0"

    // for antlr tasks
	id 'antlr'

    // useful utilities
    id "com.arakelian.multitool" version "1.5.3"
}

group   = 'com.arakelian'

task wrapper(type: Wrapper) {
	gradleVersion = '4.10.2'
}

dependencies {
	antlr 'org.antlr:antlr4:4.7.1'
    compile 'org.antlr:antlr4-runtime:4.7.1'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java"]
        }
    }
    lexer {
        antlr {
            srcDirs = ['src/lexer/antlr']
        }
    }
}

generateGrammarSource {
	// overwrites anything in src/main/java!
	outputDirectory = new File(projectDir, 'src/main/java')
	arguments += ["-visitor","-lib","src/lexer/java/com/arakelian/graphql/parser"]
}

generateLexerGrammarSource {
	outputDirectory = new File(projectDir, 'src/lexer/java')
}

generateGrammarSource.dependsOn generateLexerGrammarSource

