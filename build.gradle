// graphql-parser

buildscript {
    repositories {
      jcenter()
    }
}

plugins {
	id 'java'
	id 'maven'
    id 'signing'
	id 'eclipse'
	id 'idea'

    // keep dependencies up-to-date!
    id 'com.github.ben-manes.versions' version '0.14.0'

    // useful for creating immutable java beans
    id 'org.inferred.processors' version '1.2.3'

    // for deployment to Maven Central
    id "io.codearte.nexus-staging" version "0.8.0"

    // for antlr tasks
	id 'antlr'
}

group   = 'com.arakelian'

apply from: "gradle/core.gradle"

// this snippet is used for deployment to Maven Central
// note: nexus credentials are kept in ~/.gradle/gradle.properties
if (project.hasProperty('nexusUsername')) {
    uploadArchives {
        repositories {
            mavenDeployer {
                pom.project {
                    url "https://github.com/arakelian/${project.name}"
                    scm {
                        connection "scm:git:https://github.com/arakelian/${project.name}.git"
                        developerConnection "scm:git:git@github.com:arakelian/${project.name}.git"
                        url "https://github.com/arakelian/${project.name}.git"
                    }
                }
            }
        }
    }
}

dependencies {
	antlr 'org.antlr:antlr4:4.7'
    compile 'org.antlr:antlr4-runtime:4.7'
}

sourceSets {
    main {
        java {
            srcDirs = ["src/main/java", "src/lexer/java"]
        }
    }
    lexer {
        antlr {
            srcDirs = ['src/lexer/antlr']
        }
    }
}

generateGrammarSource {
	// overwrites anything in src/main/java!
	outputDirectory = new File(projectDir, 'src/main/java')
	arguments += ["-visitor","-lib","src/lexer/java/com/arakelian/graphql/parser"]
}

generateLexerGrammarSource {
	outputDirectory = new File(projectDir, 'src/lexer/java')
}

generateGrammarSource.dependsOn generateLexerGrammarSource

